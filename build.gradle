plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'io.freefair.lombok' version '8.6'
    id 'signing'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    mavenCentral()
    maven {
        url = 'https://api.modrinth.com/maven'
    }
    maven {
        url = 'https://libraries.minecraft.net/'
    }
    mavenLocal()
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

minecraft {
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'

    copyIdeResources = true
    accessTransformer = project.files('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            property 'forge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', project.mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation jarJar('io.github.tt432.chin:chin:[21.0.3,)')
    var imguiVersion = '1.86.12'
    compileOnly("io.github.spair:imgui-java-binding:$imguiVersion")
    compileOnly("io.github.spair:imgui-java-lwjgl3:$imguiVersion")

    implementation 'io.github.tt432:imgui4mc:1.0.0'

    jarJar minecraftLibrary('io.github.dmlloyd:jdk-classfile-backport:24.0')

//    compileOnly('maven.modrinth:sodium:mc1.21-0.6.0-beta.2-neoforge')
    compileOnly('maven.modrinth:oculus:1.20.1-1.8.0')
    compileOnly('maven.modrinth:embeddium:0.3.31+mc1.20.1')

//    implementation("maven.modrinth:physicsmod:3.0.20")

//    implementation "org.lwjgl:lwjgl-assimp:3.3.3"
//    runtimeOnly "org.lwjgl:lwjgl-assimp:3.3.3:natives-windows"
}

jarJar.enable()

signing {
    sign publishing.publications
}

java {
    withSourcesJar()
    withJavadocJar()
}

var generateModMetadata = tasks.register('generateModMetadata', ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version          : forge_version,
            forge_version_range    : forge_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from 'src/main/templates'
    into 'build/generated/sources/modMetadata'
}
sourceSets.main.resources.srcDir generateModMetadata
processResources.dependsOn(generateModMetadata)

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = project.mod_name
                description = project.mod_description
                url = "https://github.com/${githubUserName}/${githubRepoName}"

                scm {
                    connection = "scm:git:git://github.com/${githubUserName}/${githubRepoName}.git"
                    developerConnection = "scm:git:ssh://github.com/${githubUserName}/${githubRepoName}.git"
                    url = "https://github.com/${githubUserName}/${githubRepoName}"
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = "https://github.com/${githubUserName}/${githubRepoName}/tree/master/LICENSE"
                    }
                }

                developers {
                    developer {
                        id = 'tt432'
                        name = 'Chin'
                        email = '2437315224@qq.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.findProperty('ossrhUsername')
                password = project.findProperty('ossrhPassword')
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

tasks.register('runWithRenderDoc', Exec) {
    def javaExecTask = tasks.withType(JavaExec).named('runClient').get()
    def javaHome = javaExecTask.javaLauncher.get().metadata.installationPath.asFile.absolutePath

    commandLine = [
            'E:\\RenderDoc\\renderdoccmd.exe',
            'capture',
            '--opt-hook-children',
            '--wait-for-exit',
            '--working-dir',
            '.',
            "$javaHome/bin/java.exe",
            '-Xmx64m',
            '-Xms64m',
            '-Dorg.gradle.appname=gradlew',
            "-Dorg.gradle.java.home=$javaHome",
            '-classpath',
            'gradle/wrapper/gradle-wrapper.jar',
            'org.gradle.wrapper.GradleWrapperMain',
            'runClient'
    ]
}
